/*
 * Copyright (c) 2007-2014 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

buildscript {
  repositories {
    maven { url 'http://repo.spring.io/plugins-release' }
  }
  dependencies {
    classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.5'
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

ext.hadoopVersion = "2.4.1"
ext.tezVersion = "0.5.1"
ext.cascadingVersion = "3.0.0-wip-dev"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'http://conjars.org/repo/' }
  maven { url 'https://repository.apache.org/content/repositories/releases/' }
}

dependencies {
  compile group: 'cascading', name: 'cascading-core', version: cascadingVersion
  compile group: 'cascading', name: 'cascading-xml', version: cascadingVersion
  compile group: 'cascading', name: 'cascading-hadoop2-tez', version: cascadingVersion
  compile group: 'cascading', name: 'cascading-local', version: cascadingVersion

  provided( group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: hadoopVersion )
  provided( group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion )
  provided( group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopVersion )

  // these runtime deps allow the sample app to be run in an ide

  runtime group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
  runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.2'

  // hadoop deps
  runtime group: 'org.apache.tez', name: 'tez-api', version: tezVersion
  runtime group: 'org.apache.tez', name: 'tez-dag', version: tezVersion
  runtime group: 'org.apache.tez', name: 'tez-mapreduce', version: tezVersion
}

jar {
  description = "Assembles a Hadoop ready jar file"
  doFirst {
    into( 'lib' ) {
      from configurations.compile
    }
  }

  manifest {
    attributes( "Main-Class": "${archivesBaseName}/Main" )
  }
}

task distCopy( type: Copy, dependsOn: jar ) {

  into "${buildDir}/dist/${archivesBaseName}"

  from 'README.TXT'
  from 'build.gradle'
  from( 'src' ) { into 'src' }
  from( 'data' ) { into 'data' }

  from "$buildDir/libs"

}

task dist( type: Tar, dependsOn: distCopy ) {

  compression = "GZIP"
  classifier = new SimpleDateFormat( "yyyyMMdd" ).format( new Date() )

  from "${buildDir}/dist/"
}
